plugins {
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.21'
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.3.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'
	id 'com.google.protobuf' version '0.8.8'
	id 'groovy'
	id "org.liquibase.gradle" version "2.0.1"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'kotlin-kapt'

group = 'com.bootapp.dal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
	maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
	mavenCentral()
}

sourceSets {
	main {
		proto { srcDir '../protos/dal/core' }
	}
}

def queryDslVersion = '4.2.1'

dependencies {
	// kotlin
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	//jpa
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation "com.querydsl:querydsl-core:${queryDslVersion}"
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	kapt "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
	implementation 'org.mindrot:jbcrypt:0.4'
	//grpc
	implementation 'io.github.lognet:grpc-spring-boot-starter:3.2.0'
	//jdbc
	implementation 'mysql:mysql-connector-java'
	implementation 'io.shardingsphere:sharding-jdbc-spring-boot-starter:3.1.0'
	implementation 'org.liquibase:liquibase-core'
	//******** test
	testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
	testCompile 'org.spockframework:spock-spring:1.3-groovy-2.5'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'com.bootapp.dal:dal-core:0.0.1-SNAPSHOT'
	testCompile 'com.h2database:h2:1.4.199'
	testCompile 'io.grpc:grpc-testing:1.19.0'
}

protobuf {
	protoc {artifact = 'com.google.protobuf:protoc:3.5.1-1'}
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.19.0" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {outputSubDir = "java"} }
	}
	generatedFilesBaseDir = "$projectDir/src/"
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}
